plugins {
    id "io.qameta.allure" version "2.8.1"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'checkstyle'
apply plugin: 'pmd'

group 'org.fundacionjala.api'
version '1.0'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'log4j', name: 'log4j', version: '1.2.17'
    compile group: 'io.rest-assured', name: 'rest-assured', version: '4.3.0'
    compile group: 'io.rest-assured', name: 'json-schema-validator', version: '4.3.0'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.10.3'
    compile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
    compile group: 'com.jayway.jsonpath', name: 'json-path', version: '2.1.0'
    testCompile group: 'io.cucumber', name: 'cucumber-java', version: '4.8.1'
    testCompile group: 'io.cucumber', name: 'cucumber-testng', version: '4.8.1'
    testCompile group: 'io.cucumber', name: 'cucumber-picocontainer', version: '4.8.1'
    testCompile group: 'io.qameta.allure', name: 'allure-cucumber4-jvm', version: '2.13.2'
}

allure {
    version = '2.13.2'
}

checkstyle {
    toolVersion = '8.23'
    configFile = file("${rootProject.rootDir}/config/checkstyle/checkstyle.xml")
}

pmd {
    toolVersion = '6.0.0'
}

tasks.withType(Pmd) {
    ruleSets = ["java-basic", "java-braces", "java-unnecessary", "java-unusedcode", "java-strictexception"]
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

test {
    testLogging {
        events 'PASSED', 'STARTED', 'FAILED', 'SKIPPED'
    }
}

// Task for execute test using command line.
task executeBDDTests(type: Test) {
    useTestNG()
    options {
        jvmArgs = ['-Dcucumber.options= --glue org.fundacionjala.api ' +
                           '--plugin pretty ' +
                           '--plugin io.qameta.allure.cucumber4jvm.AllureCucumber4Jvm ' + cucumberOptions]
    }
    // Flag to show console output.
    testLogging.showStandardStreams = true
    // Flag to scan for classes with @Test annotation.
    scanForTestClasses = false
    // Calls allure report generation task.
    finalizedBy 'allureReport'
}
